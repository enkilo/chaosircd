# $Id: configure.in,v 1.16 2006/09/28 09:47:03 roman Exp $
# ===========================================================================
#
# GNU autoconf template for chaosircd
#
# initialize autoconf (require version 2.5x)
# ---------------------------------------------------------------------------
AC_PREREQ(2.59)
AC_INIT([chaosircd],[2.2.1])
AC_CONFIG_SUBDIRS([libchaos])

# check for compiling modes
# ---------------------------------------------------------------------------
AC_CHECK_COLOR

# set some info stuff
# ---------------------------------------------------------------------------
AC_SET_ARGS
PACKAGE_RELEASE="peace & love hippie edition"
AC_DEFINE_UNQUOTED(PACKAGE_RELEASE, "$PACKAGE_RELEASE", [Define to the release name of this package.])

VERSION=${PACKAGE_VERSION%%[[a-z]]*}

VERSION_MAJOR=${VERSION%%.[[0-9]]*}
VERSION_MINOR=${VERSION#*[0-9].}
VERSION_MINOR=${VERSION_MINOR%.[[0-9]]*}
VERSION_PATCH=${VERSION##*[0-9].}

AC_SUBST(VERSION_MAJOR)
AC_SUBST(VERSION_MINOR)
AC_SUBST(VERSION_PATCH)

CREATION=$(date)
PLATFORM=$(uname -a)
AC_DEFINE_UNQUOTED(CREATION, "$CREATION", [Creation time of this server])
AC_DEFINE_UNQUOTED(PLATFORM, "$PLATFORM", [Platform this server runs on])

# set directories
# ---------------------------------------------------------------------------
AC_CONFIG_HEADERS([config.h include/chaosircd/config.h])
AC_CONFIG_SRCDIR([src/ircd.c])

test "$prefix" = "NONE" && prefix=${ac_default_prefix}
test "$exec_prefix" = "NONE" && exec_prefix=${prefix}

eval sysconfdir=${sysconfdir}
case $sysconfdir in
  *ircd*) sysconfdir=${sysconfdir} ;;
  *) sysconfdir=${sysconfdir}/chaosircd ;;
esac

eval libexecdir=${libexecdir}
eval localstatedir=${localstatedir}

eval pidfile=${localstatedir}/run/chaosircd.pid
AC_ARG_WITH([pidfile], [  --with-pidfile=PATH     location of the pid-file [[LOCALSTATEDIR/run/chaosircd.pid]]], 
[if test "$withval" != "yes" && test "$withval" != "no"; then eval pidfile=$withval; fi])
AC_SUBST([pidfile])

#if test "$prefix" = "NONE"; then
#  prefix=$ac_default_prefix
#fi
#eval prefix=${prefix}
#eval libdir=${libdir}

case $prefix in
  *ircd*)
    plugindir=${libdir}
    ;;
  *)
    plugindir=${libdir}/chaosircd
    ;;
esac
AC_ARG_WITH([plugindir], [  --with-plugindir=PATH   location of the plugins [[LIBDIR/chaosircd]]], 
[if test "$withval" != "yes" && test "$withval" != "no"; then eval plugindir=$withval; fi])
AC_SUBST([plugindir])

case $prefix in
  *ircd*)
    logdir=${prefix}/logs
    ;;
  *)
    logdir=${localstatedir}/log
    ;;
esac
AC_ARG_WITH([logdir], [  --with-logdir=PATH   location of the logs [[LOCALSTATEDIR/log]]],
[if test "$withval" != "yes" && test "$withval" != "no"; then eval logdir=$withval; fi])
AC_SUBST([logdir])

case $prefix in
  *ircd*)
    eval "inidir=${localstatedir}/lib"
    ;;
  *)
    eval "inidir=${localstatedir}/lib/chaosircd"
    ;;
esac
AC_ARG_WITH([inidir], [  --with-inidir=PATH   location of the inis [[SHAREDSTATEDIR/ini]]],
[if test "$withval" != "yes" && test "$withval" != "no"; then eval inidir=$withval; fi])
AC_SUBST([inidir])

# set extensions and flags
# ---------------------------------------------------------------------------
SHEXT="so"
DFLAGS="-g -ggdb"
WFLAGS="-Wall -Werror"
FFLAGS=""

# check for libs and programs needed to build tichu-server
# ---------------------------------------------------------------------------
AC_DIETLIBC([/opt/diet /usr/diet])
AC_PROG_CC([cc gcc])
AC_PROG_AR
AC_PROG_RANLIB
AC_PROG_INSTALL
AC_PROG_LN
AC_PROG_DLLWRAP
AC_PROG_LEX
AC_PROG_YACC
AC_CHECK_PROG([CYGPATH], [cygpath])
AC_CHECK_PROG([NASM], [nasm], [$as_dir/$ac_word$ac_exec_ext], [], [$PATH])

# check some system dependencies
# ---------------------------------------------------------------------------
AC_CHECK_LIB([m], [cos])

AC_CANONICAL_HOST

AC_CONFIG_HOST
AC_CONFIG_DYLIB

AC_CHECK_HEADERS([fcntl.h io.h signal.h unistd.h sys/ioctl.h sys/mman.h sys/resource.h sys/socket.h sys/time.h sys/timeb.h sys/wait.h dlfcn.h netinet/in.h cygwin/in.h])
AC_FUNC_MMAP

dnl AC_CHECK_SSL
dnl AC_CHECK_FT2
dnl AC_CHECK_PSQL
dnl AC_CHECK_MYSQL

dnl AC_CHECK_FUNC([ltoa], AC_DEFINE_UNQUOTED([HAVE_LTOA], [Define this if you have the ltoa() function (defined in stdlib.h usually)]))
dnl AC_CHECK_FUNC([lltoa], AC_DEFINE_UNQUOTED([HAVE_LLTOA], [Define this if you have the ltoa() function (defined in stdlib.h usually)]))

dnl AC_C_BIGENDIAN([ENDIAN="ENDIAN_BIG"],
dnl                [ENDIAN="ENDIAN_LIL"])
    
dnl AC_SUBST(ENDIAN)
dnl AC_DEFINE_UNQUOTED(ENDIAN_LIL, 0, [Little endian machine])
dnl AC_DEFINE_UNQUOTED(ENDIAN_BIG, 1, [Big endian machine])
dnl AC_DEFINE_UNQUOTED(ENDIAN, $ENDIAN, [Define your machines endian])

IA32INLINE="0"

# check for winsuck
dnl AC_CHECK_HEADER([winsock2.h])

# FIXME: must be declared as pascal call and must have 2 arguments (8 bytes)
dnl AC_CHECK_LIB([ws2_32], [WSAStartup])
dnl AC_CHECK_LIB([wsock32], [WSAStartup])


dnl ac_cv_LIBS="$LIBS"
dnl LIBS="$LIBS -lws2_32"

dnl AC_MSG_CHECKING([for winsock2])
dnl AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[#include <winsock2.h>]], [[dnl   WSAStartup(0,0)]])],[dnl   AC_MSG_RESULT(yes)],[dnl   AC_MSG_RESULT([no])
dnl   LIBS="$ac_cv_libs"])
dnl
dnl # runtime linker
dnl case "$host" in
dnl   *darwin*)
dnl     PIE_LOADER="dlfcn_darwin"
dnl     ;;
dnl   *mingw32*)
dnl     PIE_LOADER="dlfcn_win32"
dnl     ;;
dnl esac

dnl AC_SUBST([DLFCN_LDADD])

# set debugging stuff after compiler test (-Werror could fuck it up)
# ---------------------------------------------------------------------------
AC_CHECK_DEP
AC_CHECK_DEBUG
AC_CHECK_MAINTAINER

# ---------------------------------------------------------------------------
case $libdir in
  /lib | /usr/lib) ;;
  *)
    LDFLAGS="${LDFLAGS:+$LDFLAGS }-Xlinker -rpath=\$(libdir)"
    ;;
esac

case $host in
  *-linux*)
    LDFLAGS="${LDFLAGS:+$LDFLAGS }-rdynamic"
    ;;
esac

AC_SUBST([LDFLAGS])
AC_SUBST([CPPFLAGS])

# library subdirectories
# ---------------------------------------------------------------------------
AC_CONFIG_FILES([
Makefile
config.mk
build.mk
conf/Makefile
conf/children.conf
conf/classes.conf
conf/inis.conf
conf/ircd.conf
conf/logs.conf
conf/modules.conf
conf/opers.conf
conf/ssl.conf
contrib/Makefile
contrib/debian/Makefile
doc/Makefile
include/Makefile
include/chaosircd/Makefile
m4/Makefile
modules/Makefile
modules/chanmode/Makefile
modules/lclient/Makefile
modules/msg/Makefile
modules/service/Makefile
modules/stats/Makefile
modules/usermode/Makefile
src/Makefile
test/Makefile
tools/Makefile
tools/chaosircd-config
tools/mkca
tools/mkcrt
tools/mkkeys
tools/mkreq
tools/openssl.cnf
tools/sign])

AC_CONFIG_STATUS

# be verbose :)
# ---------------------------------------------------------------------------
AC_SUMMARIZE([prefix bindir datadir includedir],
             [COLOR DEP DEBUG |
              CC CFLAGS LDFLAGS LIBS |
              host build],
             "%20s: %s")
