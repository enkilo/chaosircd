# $Id: modules.m4,v 1.1 2006/09/27 10:10:38 roman Exp $
# ===========================================================================
#
# Dynamic subdir configuration
#
# Copyleft GPL (c) 2005 by Roman Senn <smoli@paranoya.ch>
#


# AC_GEN_MODULE_VAR([MODULE], [VARNAME], [PATTERN], [SED-SCRIPT])
# 
# generates shell script that lists files in subdir and puts them
# in a Makefile variable
# ---------------------------------------------------------------------------
AC_DEFUN([AC_GEN_MODULE_VAR],
[echo -n "$1_$2 = " >&3
files=\`ls \$srcdir/$1/$3 2>/dev/null | sed -e "$4"\`
for f in \$files
do
  echo " \\\\" >&3
  echo -n "\$f" >&3
done
echo >&3
echo >&3
])

AC_DEFUN([AC_CONFIG_MODULES],
[m4_define([AC_MODULE_LIST], [m4_normalize($1)])dnl
dnl m4_define([AC_MODULES], [m4_split(AC_MODULE_LIST)])
dnl echo AC_MODULE_LIST
m4_foreach([MOD], m4_split(AC_MODULE_LIST),
[config_files="$config_files MOD/Makefile"
m4_define([MODFILE], MOD)
m4_append([MODFILE], [[/]Makefile])
AC_CONFIG_FILES(MODFILE)])
MODULES="AC_MODULE_LIST"
AC_GEN_CONFIG_MODULE
AC_SUBST(MODULES)])

# generates config.module script
AC_DEFUN([AC_GEN_CONFIG_MODULE],
[CONFIG_MODULE="config.module"
MK_MODULE="module.mk"
AC_MSG_NOTICE([creating ./$CONFIG_MODULE])
cat > $CONFIG_MODULE << M4EOF
#! /bin/sh
# \$Id: modules.m4,v 1.1 2006/09/27 10:10:38 roman Exp $
# Generated by $as_me.
# Run this file to recreate the current module configuration.
MODULES="$MODULES"
MKFILE="$MK_MODULE"
srcdir="."

if test "\$[1]" = "--srcdir"; then
  shift
  srcdir="\$[1]"
  shift
fi

echo \$srcdir

if test "\$[1]" = "--quiet"; then
  exec 1<>/dev/null
  shift
fi

echo -n "$CONFIG_MODULE: configuring..."

if test "\$[*]"; then
  for module in "\$[*]"
  do
    case \$module in
      m4_foreach([MOD], m4_split(AC_MODULE_LIST), MOD[)][
dnl        [output_]MOD="yes"
        BUILD="\$BUILD MOD"
        ;;
      ])[*][)]
        ;;
    esac
  done
  exec 3<>/dev/null
else
  BUILD="\$MODULES"
  exec 3<>\$MKFILE
fi
cat >&3 << EOF
# libowfat module configuration file
#
# \\\$Id: modules.m4,v 1.1 2006/09/27 10:10:38 roman Exp $

# all the modules
# ---------------------------------------------------------------------------
MODULES = \$MODULES

# all the functions
# ---------------------------------------------------------------------------
FUNCTIONS = m4_foreach([MOD], m4_split(AC_MODULE_LIST), \\\\
[[\\\$(]MOD[_FUNCTIONS) ]])

# all the function objects
# ---------------------------------------------------------------------------
OBJECTS = m4_foreach([MOD], m4_split(AC_MODULE_LIST), \\\\
[[\\\$(]MOD[_OBJECTS) ]])
LIBOBJECTS = m4_foreach([MOD], m4_split(AC_MODULE_LIST), \\\\
[[\\\$(]MOD[_LIBOBJECTS) ]])

# all the manuals
# ---------------------------------------------------------------------------
MANS = m4_foreach([MOD], m4_split(AC_MODULE_LIST), \\\\
[[\\\$(]MOD[_MANS) ]])

EOF
for mod in \$BUILD
do
  cat >&3 << EOF
# Module $mod
# ---------------------------------------------------------------------------
EOF
AC_GEN_MODULE_VAR([\${mod}], [FUNCTIONS], [*.c], [s,^.*/,,;;s,\.c$,,])
AC_GEN_MODULE_VAR([\${mod}], [OBJECTS], [*.c], [s,^.*/,,;;s,\.c$,\.o,])
AC_GEN_MODULE_VAR([\${mod}], [LIBOBJECTS], [*.c], [s,^.*/,\${mod}/,;;s,\.c$,\.o,])
AC_GEN_MODULE_VAR([\${mod}], [MANS], [*.[[0-9]]], [s,^.*/,,])
echo >&3

  dir_suffix=\$mod
  builddir="."
  top_builddir=".."
    
  case \$srcdir in
    .)  # No --srcdir option.  We are building in place.
      srcdir="."
      top_srcdir=".."
      ;;
    [[\\/]]* | ?:[[\\/]]* )  # Absolute path.
      srcdir=\$srcdir\$dir_suffix;
      top_srcdir=\$srcdir
      ;;
    *) # Relative path.
      srcdir=\$top_builddir\$srcdir\$dir_suffix
      top_srcdir=\$top_builddir\$srcdir
      ;;
  esac
      

cat > \$mod/Makefile << EOF
AC_MOD_MAKEFILE([\${mod}])
EOF
echo -n -e " ${COLOR_DIR}\$mod${NC}"
done
echo
M4EOF
chmod +x $CONFIG_MODULE


if test -n "$srcdir"; then
  config_module_args="--srcdir $srcdir"
fi

./$CONFIG_MODULE $config_module_args
])


AC_DEFUN([AC_MOD_MAKEFILE], 
[# \$Id: modules.m4,v 1.1 2006/09/27 10:10:38 roman Exp $
# ===========================================================================
#
# GNU Makefile for libowfat $1 library
#
# relative directories
# ---------------------------------------------------------------------------
thisname             = \$thisname
srcdir               = \$srcdir
top_srcdir           = \$top_srcdir
builddir             = \$builddir
top_builddir         = \$top_builddir

# include modules.mak
# ---------------------------------------------------------------------------
include \\\$(top_builddir)/module.mk

# ---------------------------------------------------------------------------
FUNCTIONS            = \\\$([$1]_FUNCTIONS)
SOURCES              = \\\$(FUNCTIONS:%=%.c)
OBJECTS              = \\\$(FUNCTIONS:%=%.o)
DEPS                 = \\\$(FUNCTIONS:%=%.d)
MANS                 = \\\$([$1]_MANS)

ARCHIVE              = [$1].a
man_DATA             = \\\$(MANS)

module: Makefile \\\$(ARCHIVE)
install: install-data

\\\$(srcdir)/Makefile: \\\$(top_builddir)/config.module
	@\\\$(ECHO_CONFIG)
	\\\$(top_builddir)/config.module [$1]

# include config.mak (makefile helper) if there
# ---------------------------------------------------------------------------
include \\\$(top_builddir)/build.mk])
